note 'Httpbin service - simply returns all data you sent back to you.'

type 'ParamsDictionary' dictionary('string', 'string')

type 'BaseHttpBinResponse' message(skipUnknownFields: true) {
  args   'ParamsDictionary'
  data   'string'
  origin 'string'
  url    'string'
}

// Form request + response.
type 'Person' message {
  name 'string'
  age  'int32'
}

type 'PersonResponse' message(parent: 'BaseHttpBinResponse', skipUnknownFields: true) {
  form 'Person'
}

// Form post service with test.
service {

  name "FormService"
  location "http://httpbin.org"

  post "/post" spec {
    response "PersonResponse"
    body form('Person')
  }

  describe 'Send Peter' spec {
    it('should pass') {
      def peterResp = service.post '/post' with {
        body form {
          name 'Peter Heel'
          age  '5'
        }
      }
      peterResp.mustSucceed()

      def resName = peterResp.body?.form?.name
      def resAge = peterResp.body?.form?.age

      assert resName == "Peter Heel"
      assert resAge == 5
    }
  }
}

// Bytes post service with test.
service {

  name "BytesService"
  location "http://httpbin.org"

  post "/post" spec {
    response 'BaseHttpBinResponse'
    body data()
  }

  describe 'Send dragon bytes' spec {
    it('should pass') {
      def dragonBytes = "Dragon".getBytes()

      def res = service.post "/post" with {
        body bytes(dragonBytes)
      }

      res.mustSucceed()

      assert res.body?.data.getBytes() == dragonBytes
    }
  }
}

// Text and file upload service.
type 'UploadedText' message {
  text 'string'
}

type 'UploadedFile' message {
  file 'string'
}

type 'UploadTextAndFileResponse' message(parent: 'BaseHttpBinResponse', skipUnknownFields: true) {
  form 'UploadedText'
  files 'UploadedFile'
}

service {
  name "MultipartService"
  location "http://httpbin.org"

  post "/post" spec {
    response "UploadTextAndFileResponse"
    body multipart('form-data') {
      text 'string'
      file file()
    }
    parameters {
      param 'string'
    }
  }

  describe "upload pig" spec {
    File f = new File("$baseDir/pig.png")
    System.err.println f.getAbsolutePath()

    String pigName = 'This is a pig.'

    def res = service.post "/post" with {
      parameters {
        param 'value'
      }
      body multipart {
        file f
        text pigName
      }
    }

    it("must succeed") { res.mustSucceed() }
    it("submits text") { res.body?.form?.text.contains("This is a pig.") }
    it("submits file") { res.body?.files?.file?.contains(f.bytes.encodeBase64().toString()) }
    it("submits parameters") { res.body?.args?.param == 'value' }
  }

}

service {
  // TODO: Think how we can get an alternative.
  name "ParamsInPathService"
  location "http://httpbin.org"

  post '/post?name1=value1' spec {
    response 'BaseHttpBinResponse'
    parameters {
      name2 'string'
    }
  }

  describe "testing" spec {
    it('should pass') {
      def resp = service.post '/post?name1=value1' with {
        parameters {
          name2 'value2'
        }
      }
      resp.mustSucceed()
      assert resp.body?.args?.name1 == 'value1'
      assert resp.body?.args?.name2 == 'value2'
    }
  }

}
