note "Twitter API"

def TIMESTAMP_FORMAT = "EEE MMM dd HH:mm:ss Z yyyy"

type "timestamp" spec {
  description "Date and time"
  from("json") {
    asDate(TIMESTAMP_FORMAT)
  }
  to("json") {
    asDate(TIMESTAMP_FORMAT)
  }
}

type 'User' message(skipUnknownFields: true) {
  id_str 'string'
  name 'string'
  profile_image_url 'string'
  is_translator 'bool'
  location 'string'
  created_at 'timestamp'
  favourites_count 'string' // must be 'int32' or int
  url 'string' optional
  utc_offset 'int32' optional
  screen_name 'string'
}

type "Base" message {
  id 'int64'
}

type "Tweet" message(parent: "Base") {
  //id 'int64'
  text 'string'
  created_at 'timestamp'
  favorited 'bool'
  favorite_count 'int32'
  truncated 'bool'
  retweeted 'bool'
  possibly_sensitive 'bool' optional
  retweet_count 'int32'
  in_reply_to_user_id 'int64' optional
  in_reply_to_status_id 'int64' optional
  in_reply_to_screen_name 'string' optional
  user 'User'
}

type "SearchResponse" message {
  statuses 'Tweet' sequence
  search_metadata(skip: true)
}

service {

  name "Twitter"

  version "1.1"
  location "https://api.twitter.com/$version"

  get "search/tweets.json" spec {
    parameters {
      q(type: 'string', examples: ['@noradio'])
      count 'int32' optional
      include_entities 'bool' optional
    }
    response 'SearchResponse'
  }

  tests {
    httpHeaders {
      'Authorization' "Bearer AAAAAAAAAAAAAAAAAAAAAJ3PVgAAAAAA%2BPcuieQDv6hOE9SyWM2AOWjIloc%3DU2fCluURs5dnG5A3WaaVhNgiBjXKkV5lynvoquGu7ediOCRWiF"
    }
    useExamples true

    scenario "search for tweets" spec {
      def searchResponse = get "search/tweets.json" with {
        parameters {
          q '#androiddev'
          count 2
          include_entities false
        }
      }

      assert searchResponse.body != null

      def size = searchResponse.body?.statuses?.size()
      assert size == 2 : "Expected 2 statuses, got $size"
      assert searchResponse.body?.statuses?.every { it.user != null }
    }

  }

}
